buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
	}
}
apply plugin: 'io.codearte.nexus-staging'


if (project.hasProperty('release')) {

	subprojects {

		task javadocJar(type: Jar) {
			classifier = 'javadoc'
			from javadoc
		}

		task sourcesJar(type: Jar) {
			classifier = 'sources'
			from sourceSets.main.allSource
		}

		artifacts {
			archives javadocJar, sourcesJar
		}
	}

	allprojects {
		apply plugin: 'signing'
		apply plugin: 'maven'

		// Signature of artifacts
		signing {
			sign configurations.archives
		}

		// OSSRH publication
		uploadArchives {
			repositories {
				mavenDeployer {
					// POM signature
					beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
					// Target repository
					repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
						authentication(userName: ossrhUser, password: ossrhPassword)
					}
					pom.project {
						name project.name
						description project.description
						packaging 'jar'
						url 'https://github.com/atsu85/openapi-snapshots'

						scm {
							connection 'scm:git:https://github.com/atsu85/openapi-snapshots.git'
							developerConnection 'scm:git:git@github.com:atsu85/openapi-snapshots.git'
							url 'https://github.com/atsu85/openapi-snapshots.git'
						}

						licenses {
							license {
								name 'The Apache License, Version 2.0'
								url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
								distribution 'repo'
							}
						}

						developers {
							developer {
								id = 'atsu85'
								name = 'Ats Uiboupin'
								email = 'ats.uiboupin@gmail.com'
							}
						}
					}
				}
			}
		}

	}
}

nexusStaging {
	username = ossrhUser
	password = ossrhPassword
}
